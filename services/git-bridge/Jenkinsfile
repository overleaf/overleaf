pipeline {
  agent {
    node {
      label 'jenkins-agent-web'
      customWorkspace '/workspace'
    }
  }
  options {
    timestamps()
    timeout(time: 15, unit: 'MINUTES')
  }
  environment {
    BRANCH_NAME = "${env.CHANGE_BRANCH ? env.CHANGE_BRANCH : env.BRANCH_NAME}"
    JENKINS_BUILD_NUMBER = "${BUILD_NUMBER}"
  }
  stages {
    stage('Set Build Variables') {
      steps {
        script {
          def relevantCommitHash
          if (env.CHANGE_BRANCH) {
            def commitExistsOnRemote = sh(script: "git branch --remotes --contains ${GIT_COMMIT}", returnStdout: true).trim()
            if (commitExistsOnRemote) {
              echo "PR build detected, but commit exists on remote. Using ${GIT_COMMIT}"
              relevantCommitHash = "${GIT_COMMIT}"
            } else {
              def parentCommits = sh(script: 'git rev-parse HEAD^@', returnStdout: true).trim().split('\n')
              if (parentCommits.size() >= 2) {
                echo "PR build detected. Jenkins checked out a merge commit: ${GIT_COMMIT} (parents: ${parentCommits.join(', ')})"
                relevantCommitHash = parentCommits[0]
                echo "Using first parent (branch commit): ${relevantCommitHash}"
              } else {
                echo "WARN: PR build detected, but ${GIT_COMMIT} is neither a merge commit, nor does it exist on the remote."
                relevantCommitHash = "${GIT_COMMIT}"
              }
            }
          } else {
            echo "Branch build detected. Using commit: ${GIT_COMMIT}"
            relevantCommitHash = "${GIT_COMMIT}"
          }
          env.COMMIT_SHA = relevantCommitHash
          env.SHORT_SHA = relevantCommitHash.take(7)
          env.BUILD_NUMBER = "${env.SHORT_SHA}_${env.JENKINS_BUILD_NUMBER}"
        }
      }
    }
    stage('Stage 1') {
      parallel {
        stage('Build') {
          steps {
            dir('services/git-bridge') {
              sh 'make refresh_cache -j2'
              retry(count: 3) {
                sh 'make docker_build_base'
              }
            }
          }
        }
      }
    }
    stage('Stage 2') {
      parallel {
        stage('Build production and push') {
          steps {
            dir('services/git-bridge') {
              retry(count: 3) {
                sh 'make docker_build'
              }
              sh 'make push_branch'
            }
          }
        }
        stage('Format Java') {
          steps {
            dir('services/git-bridge') {
              sh 'make docker_format'
            }
          }
        }
        stage('Test') {
          steps {
            dir('services/git-bridge') {
              retry(count: 3) {
                sh 'make docker_test'
              }
            }
          }
        }
      }
    }
    stage('Push Production') {
      steps {
        dir('services/git-bridge') {
          sh 'make push'
        }
      }
    }
  }
  post {
    // Collect junit test results for both success and failure case.
    always {
      junit checksName: 'git-bridge test results', testResults: 'services/git-bridge/target/surefire-reports/*.xml'
    }
    cleanup {
      dir('services/git-bridge') {
        sh 'make clean_ci'
      }
      sh 'make clean_jenkins -j10'
    }
  }
}